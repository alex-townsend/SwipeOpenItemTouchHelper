apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.alex-townsend'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.1"

  defaultConfig {
    minSdkVersion 11
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  sourceSets {
    androidTest {
      java.srcDirs = ['src/androidTest/java']
      resources.srcDirs = ['src/androidTest/res']
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  packagingOptions {
    exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
  }
}

dependencies {
  compile 'com.android.support:recyclerview-v7:25.1.0'

  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.android.support.test:rules:0.5'
  androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.2.2') {
    exclude module: 'support-v4'
  }
  androidTestCompile 'com.google.truth:truth:0.27'
}


// build a jar with source files
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  failOnError  false
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}